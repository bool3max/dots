#!/usr/bin/fish

# a script that's usually called via sway keybindings to change the current wallpaper
# -s, --set = simply set the curently chosen wallpaper without updating any variables
# -t, --traverse = increase the index by 1 and then set the wallpaper

argparse -n "wallscript" 's/set' 't/traverse' 'd/debug' -- $argv

function set_current_index
    # set the wallpaper to the one described by the current index
    swaymsg output '*' bg $walls[$wall_index] stretch
end

function update
    # rebuild list of all wallpapers and start over at index 1
    set -U walls $HOME/walls/*
    set -U wall_index 1
end

if not set -qU walls || not set -qU wall_index || test (count $walls) -lt (count $HOME/walls/*)
    update
end

if set -q _flag_set
    set_current_index
else if set -q _flag_traverse
    if test (math $wall_index + 1) -gt (count $walls)
        # start over if we've reached the beginning
        set -U wall_index 1
        set_current_index
        exit
    end

    set -U wall_index (math "$wall_index + 1")
    set_current_index
else if set -q _flag_debug
    set -qU walls && echo "wall database set" || echo "wall database not set"
    echo "wall_index = $wall_index --- \"$walls[$wall_index]\""
else
    echo -e "\e[1mwallscript: Operation missing! Aborting.\e[0m" 1>&2
    exit 1
end
